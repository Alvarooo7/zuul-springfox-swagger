import java.time.LocalDate
import java.time.format.DateTimeFormatter

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE"
    }
}

apply plugin: 'java'
apply plugin: "io.spring.dependency-management"
apply plugin: 'maven-publish'

// project properties
group 'ru.reliabletech'
def libName = 'zuul-springfox-swagger'
version '0.2.1'
sourceCompatibility = 1.8
def currentDate = LocalDate.now().format(DateTimeFormatter.ISO_DATE)

//tasks
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

task expandDescriptor(type: Copy) {
    from 'descriptor.json'
    into 'build'
    expand(version: project.version, date: currentDate)
}

build.dependsOn expandDescriptor
compileJava.dependsOn(processResources)

// plugin settings
publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            groupId project.group
            artifactId libName
            version project.version

        }
    }
}

jar {
    manifest {
        attributes(
                "Group": group,
                "Artifact-Id": libName,
                "Version": version,
                "Source-Compatibility": sourceCompatibility,
                "Build-Time": currentDate
        )
    }
}

//dependency management
def springCloudVersion = 'Finchley.SR1'

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}
repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compileOnly 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-server'
    compileOnly 'org.springframework.cloud:spring-cloud-starter-netflix-zuul'
    annotationProcessor 'org.projectlombok:lombok:1.18.4'
    implementation 'org.projectlombok:lombok:1.18.4'
    compile 'io.springfox:springfox-swagger2:2.9.2'
    compile 'io.springfox.ui:springfox-swagger-ui-rfc6570:1.0.0'
    compileOnly 'org.springframework.boot:spring-boot-configuration-processor:1.5.9.RELEASE'
}
