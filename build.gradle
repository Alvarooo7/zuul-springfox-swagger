import java.time.format.DateTimeFormatter

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE"
    }
}

apply plugin: 'java'
apply plugin: "io.spring.dependency-management"
apply plugin: 'maven-publish'

// project properties
group 'ru.reliabletech'
def artifact = 'zuul-springfox-swagger'
version '0.1.0'
sourceCompatibility = 1.8

// plugin settings
publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId project.group
            artifactId artifact
            version project.version
        }
    }
}

model {
    tasks.generatePomFileForMavenPublication {
        destination = file("$buildDir/libs/$artifact-${version}.pom")
    }
}

jar {
    manifest {
        attributes(
                "Group": group,
                "Artifact-Id": artifact,
                "Version": version,
                "Source-Compatibility": sourceCompatibility,
                "Build-Time": new Date()
        )
    }
}

//tasks
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

task expandDescriptor(type: Copy) {
    from 'descriptor.json'
    into 'build'
    expand(version: project.version, date: java.time.LocalDate.now().format(DateTimeFormatter.ISO_DATE))
}

build.dependsOn expandDescriptor

//dependency management
def springCloudVersion = 'Dalston.SR3'

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}
repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'org.springframework.cloud:spring-cloud-starter-eureka'
    compile 'org.springframework.cloud:spring-cloud-starter-zuul'
    compile 'org.projectlombok:lombok:1.16.18'
    compile 'io.springfox:springfox-swagger2:2.7.0'
    compile 'io.springfox.ui:springfox-swagger-ui-rfc6570:1.0.0'
}
task wrapper(type: Wrapper) {
    gradleVersion = '4.3.1'
}
